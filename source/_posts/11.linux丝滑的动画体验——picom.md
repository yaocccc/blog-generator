---
title: linux丝滑的动画体验
url: linuxpicomanimation
date: 2022/06/19 20:42:48
tags: [LINUX, PICOM, 桌面美化]
excerpt: xserver的窗口效果器 动画、透明、模糊、圆角
mermaid: false
index_img: /img/picom/picom.gif
---

<!-- markdown-toc GitLab -->

* [picom](#picom)
* [安装](#安装)
* [启动](#启动)
* [为了更更方便大家安装 我从别人那抄了pkgbuild](#为了更更方便大家安装-我从别人那抄了pkgbuild)

<!-- markdown-toc -->

## picom

[视频链接](https://www.bilibili.com/video/bv19T411G7Eq)

xserver的窗口效果合成器, fork自 compton

提供了窗口的透明、背景模糊、淡入淡出、圆角、动画等效果

[github仓库](https://github.com/yshui/picom) | [个人fork版本](https://github.com/yaocccc/picom)

[个人自用配置](https://github.com/yaocccc/scripts/blob/master/config/picom.conf)

个人fork的版本修改了什么？

在dccsillag/picom实现动画效果的基础上，增加了两个动画相关的配置 [PR#35](https://github.com/dccsillag/picom/pull/35)

## 安装

从仓库clone到本地

```shell
cd picom

rm -rf build
LDFLAGS="-L/usr/local/lib" CPPFLAGS="-I/usr/local/include" meson --buildtype=release . build
ninja -C build
sudo ninja -C build install
```

## 启动

`picom --experimental-backends --config ~/scripts/config/picom.conf`

## 为了更更方便大家安装 我从别人那抄了pkgbuild

- 将以下内容写入PKGBUILD文件
- 执行makepkg -si

```plaintext
_forkname=yaocccc
pkgname=picom-animations-git
_gitname=picom
pkgver=1786__2022.09.19
pkgrel=1
pkgdesc="X compositor (fork of compton) (git-version; animation fork)"
arch=(i686 x86_64)
url="https://github.com/${_forkname}/${_gitname}"
license=('MIT' 'MPL2')
depends=('libgl' 'libev' 'pcre' 'libx11' 'xcb-util-renderutil' 'libxcb' 'xcb-util-image' 'libxext'
         'pixman' 'libconfig' 'libdbus' 'hicolor-icon-theme')
makedepends=('git' 'mesa' 'meson' 'asciidoc' 'uthash' 'xorgproto')
optdepends=('dbus:          To control picom via D-Bus'
            'xorg-xwininfo: For picom-trans'
            'xorg-xprop:    For picom-trans'
            'python:        For picom-convgen.py')
provides=('compton' 'compton-git' 'picom')
conflicts=('compton' 'compton-git' 'picom')
replaces=('compton-git')
source=(git+"https://github.com/${_forkname}/${_gitname}.git")
sha256sums=("SKIP")

pkgver() {
    cd ${_gitname}
    _tag=$(git describe --tags | sed 's:^v::') # tag is mobile, and switches between numbers and letters, can't use it for versioning
    _commits=$(git rev-list --count HEAD) # total commits is the most sane way of getting incremental pkgver
    _date=$(git log -1 --date=short --pretty=format:%cd)
    printf "%s_%s_%s\n" "${_commits}" "${_tag}" "${_date}" | sed 's/-/./g'
}

build() {
  cd "${srcdir}/${_gitname}"
  meson --buildtype=release . build --prefix=/usr -Dwith_docs=true
  ninja -C build
}

package() {
  cd "${srcdir}/${_gitname}"

  DESTDIR="${pkgdir}" ninja -C build install

  # install license
  install -D -m644 "LICENSES/MIT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"

  # example conf
  install -D -m644 "picom.sample.conf" "${pkgdir}/etc/xdg/picom.conf.example"
}
```
